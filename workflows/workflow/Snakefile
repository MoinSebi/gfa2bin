from snakemake.utils import min_version
import pandas as pd
min_version("6.0")


###############################################################################
# Global settings
###############################################################################

# Load the config file
configfile: "config/config.yaml"

wf_basedir = workflow.current_basedir


###############################################################################
# 00 Validate inputs
###############################################################################
module validate_inputs:
    snakefile:
        "00_validate_inputs/Snakefile"
    config: config
    # prefix: "00_validate_inputs"

use rule * from validate_inputs as validate_inputs_*


###############################################################################
# 01 Prepare the fasta reference file from genome graph
###############################################################################
module prepare_fasta_ref:
    snakefile:
        "01_prepare_fasta_ref/Snakefile"
    config: config
    # prefix: "01_fasta_ref"
# Decide whether a fasta reference file should be created from the graph
if config["input_mode"] == "reads" and config["use_fasta_ref"]:
    use rule * from prepare_fasta_ref as prepare_fasta_ref_*


###############################################################################
# 02 a/b/c make/use pack files (pack), either from a fasta reference, a graph,
# or from a provided pack file list
###############################################################################
# We import both workflows here, but only one will be used
module make_packs_align2fasta_ref:
    snakefile:
        "02a_make_packs_align2fasta_ref/Snakefile"
    config: config
    # prefix: "make_packs"

module make_packs_align2fasta_ref:
    snakefile:
        "02a_make_packs_align2fasta_ref/Snakefile"
    config: config

module make_packs_from_list:
    snakefile:
        "02b_make_packs_from_list/Snakefile"
    config: config
    # prefix: "make_packs"

# Not checked/done yet
module make_packs_align2graph:
    snakefile:
        "02c_make_packs_align2graph/Snakefile"
    config: config
    # prefix: "make_packs"


# Decide which workflow to use, based on the config setting
if config["input_mode"] == "reads":
    if config["use_fasta_ref"]:
        use rule * from make_packs_align2fasta_ref as make_packs_*
    else:
        use rule * from make_packs_align2graph as make_packs_*
elif config["input_mode"] == "packs":
    use rule * from make_packs_from_list as make_packs_*
else:
    raise ValueError("Unknown input mode: " + config["input_mode"])


###############################################################################
# 03 Make fams/beds/bims from packs
###############################################################################
module make_fambedbim:
    snakefile:
        "03_make_fambedbim/Snakefile"
    config: config
    # prefix: "make_fambedbim"

use rule * from make_fambedbim as make_fambedbim_*


###############################################################################
# 04 Run the GWAS
###############################################################################
module gwas:
    snakefile:
        "04_gwas/Snakefile"
    config: config
    # prefix: "gwas"

use rule * from gwas as gwas_*


# Define the final target rule
rule target:
    input:
        rules.gwas_target.input,
    default_target: True